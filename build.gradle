plugins {
    id 'java-library'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.140'
}

base {
    archivesName = "${fileName}-${mc_version}"
}

version = mod_version
group = modGroup

java.toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url "https://maven.blamejared.com"
        content {
            includeGroup "vazkii.patchouli"
            includeGroupByRegex "net\\.darkhax.*"
            includeGroupByRegex "com\\.blamejared.*"
            includeGroup "org.openzen.zencode"
            includeGroup "mezz.jei"
        }
    }
    maven {
        // Curios
        url "https://maven.theillusivec4.top"
        content {
            includeGroupByRegex "top\\.theillusivec4.*"
        }
    }
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://maven.kosmx.dev/' }
    maven { url "https://rtxyd.github.io/maven-repo" }
    maven {
        name = "Iron's Maven - Release"
        url = "https://code.redspace.io/releases"
    }
    maven {
        name = "Iron's Maven - Snapshots"
        url = "https://code.redspace.io/snapshots"
    }
    maven { url 'https://jitpack.io' }
}

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', ''
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    // Required Dependencies
    implementation "curse.maven:irons-spells-n-spellbooks-855414:7402503"
    implementation "curse.maven:better-combat-by-daedelus-639842:7348161"

    // Dev Env Mods
    runtimeOnly "curse.maven:mmmmmmmmmmmm-225738:7206981"
    runtimeOnly "curse.maven:jei-238222:7420587"
    runtimeOnly "curse.maven:jade-324717:7428884"

    // Libs
    runtimeOnly "curse.maven:cloth-config-348521:5729127"
    runtimeOnly "curse.maven:curios-309927:6529130"
    runtimeOnly "curse.maven:playeranimator-658587:7389814"
    runtimeOnly "curse.maven:geckolib-388172:7415496"
    runtimeOnly "curse.maven:caelus-308989:5694215"
    runtimeOnly "curse.maven:selene-499980:7522321"
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                  : mod_id,
            mod_name               : modName,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : author,
            mod_description        : desc,
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : fileName,
                "Specification-Vendor"    : author,
                "Specification-Version"   : "1.0",
                "Implementation-Title"    : fileName,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

java {
    withSourcesJar()
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}