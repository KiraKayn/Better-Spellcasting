plugins {
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0.13, 6.2)'
}

base {
    archivesName = "${fileName}-${mc_version}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)

minecraft {
    mappings channel: 'official', version: mc_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            mods {
                "${modid}" {
                    source sourceSets.main
                }
            }
        }

        client {
            jvmArgs '-Xms2G', '-Xmx4G'
        }

        server {
            args "--nogui"
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', "${modid}", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        name = "Iron's Maven - Release"
        url = "https://code.redspace.io/releases"
    }
    maven {
        name = "Iron's Maven - Snapshots"
        url = "https://code.redspace.io/snapshots"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
}

tasks.named('processResources', ProcessResources).configure {
    def replaceProperties = [
            modGroup    : modGroup,
            modid       : modid,
            version     : version,
            modName     : modName,
            author      : author,
            desc        : desc,
            mcVersion   : mc_version,
            javaVersion : javaVersion,
            forgeVersion: forge_version
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : fileName,
                "Specification-Vendor"    : author,
                "Specification-Version"   : "1.0",
                "Implementation-Title"    : fileName,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy 'reobfJar'
}

java {
    withSourcesJar()
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}